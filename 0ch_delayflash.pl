#============================================================================================================
#	拡張機能 - 抑制くん
#	0ch_delayflash.pl
#	--------------------------------------------------------------------------------------------
#	かんりぶれ★ ( https://boumou.li/ )
#
#	Last up date 2024.12.16 ( 複数ソースの速報取得時の不具合修正 )
#============================================================================================================
package ZPL_delayflash;

#------------------------------------------------------------------------------------------------------------
#	拡張機能名称取得
#------------------------------------------------------------------------------------------------------------
sub getName
{
	return '抑制くん';
}

#------------------------------------------------------------------------------------------------------------
#	拡張機能説明取得
#------------------------------------------------------------------------------------------------------------
sub getExplanation
{
	return '速報BOTのスレ乱立を抑制';
}

#------------------------------------------------------------------------------------------------------------
#	拡張機能タイプ取得
#------------------------------------------------------------------------------------------------------------
sub getType
{
	return 16;
}

#------------------------------------------------------------------------------------------------------------
#	設定リスト取得 (0ch+ Only)
#------------------------------------------------------------------------------------------------------------
sub getConfig
{
	return {
		'delay'	=> {
			'default'		=> 1800,
			'valuetype'		=> 1,
			'description'	=> '前回のスレ立てから一定時間新スレを立てない(秒)',
		},
		'_samba'	=> {
			'default'		=> 0,
			'valuetype'		=> 1,
			'description'	=> 'スレ立て日時 ※一時変数なのでいじらない',
		},
	};
}

#------------------------------------------------------------------------------------------------------------
#	拡張機能実行インタフェイス
#------------------------------------------------------------------------------------------------------------
sub execute
{
	my $this = shift;
	my ($Sys, $Form, $type) = @_;
	
	# 0ch本家では実行しない
	return 0 if (!$this->{'is0ch+'});

	# news1で実行
	return 0 if (!$Sys->Equal('BBS', 'news1'));

	# ID:qNewsFlashで実行
	return 0 if ($Form->Get('idpart') !~ /^ID:qNewsFlash/);

	if ($Sys->Equal('MODE', 1)) {
		my $delay = $this->GetConf('delay');
		my $samba = $this->GetConf('_samba');
		my $nowtime = time;
		my $tt = $Form->Get('subject');
		my $infoDir = $Sys->Get('INFO');
		my $deny_tt = ".$infoDir/deny_tt.txt";

		if (check_tt($tt, $deny_tt)) {
			PrintBBSError($Sys, 500);
		
		# 前回のスレ立てからの経過時間を判定
		} elsif ($nowtime - $samba > $delay) {
			# 間引きリストリセット
			open my $fh, '>', $deny_tt;
			close $fh;
			# スレ立て時間記録
			$this->SetConf('_samba', $nowtime);
			
		} else {
			# 間引きリストに追記
			open my $fh, '>>', $deny_tt;
			print $fh "$tt\n";
			close $fh;
			PrintBBSError($Sys, 500);
		}
	}
	return 0;
}

#------------------------------------------------------------------------------------------------------------
#	間引きスレタイと照合
#------------------------------------------------------------------------------------------------------------
sub check_tt {
    my ($tt, $deny_tt) = @_;

    # 間引きスレタイリストを読み込む
    open my $fh, '<', $deny_tt;

    # ファイルの各行をチェック
    while (my $line = <$fh>) {
        chomp $line;
        if ($line eq $tt) {
            close $fh;
            return 1; 
        }
    }

    close $fh; 
    return 0;
}

#------------------------------------------------------------------------------------------------------------
#	なんちゃってbbs.cgiエラーページ表示
#------------------------------------------------------------------------------------------------------------
sub PrintBBSError
{
	my ($Sys, $err) = @_;
	
	my $CGI = $Sys->Get('MainCGI');
	my $Page = $CGI->{'PAGE'};
	
	$Page->Print("Content-type: text/html\n\n");
	$Page->Print("<html><head><title>");
	$Page->Print("ERROR!</title></head><meta charset=\"Shift_JIS\"><!--nobanner-->\n");
	$Page->Print("<body><font color=red>エラーコード:${err}</font><hr>");
	
	$Page->Flush('', 0, 0);
	
	exit($err);
}

#------------------------------------------------------------------------------------------------------------
#	コンストラクタ
#------------------------------------------------------------------------------------------------------------
sub new
{
	my $class = shift;
	my ($Config) = @_;
	
	my $this = {};
	bless $this, $class;
	
	if (defined $Config) {
		$this->{'PLUGINCONF'} = $Config;
		$this->{'is0ch+'} = 1;
	}
	else {
		$this->{'CONFIG'} = $class->getConfig();
		$this->{'is0ch+'} = 0;
	}
	
	return $this;
}

#------------------------------------------------------------------------------------------------------------
#	設定値取得 (0ch+ Only)
#------------------------------------------------------------------------------------------------------------
sub GetConf
{
	my $this = shift;
	my ($key) = @_;
	if ($this->{'is0ch+'}) {
		return $this->{'PLUGINCONF'}->GetConfig($key);
	}
	elsif (defined $this->{'CONFIG'}->{$key}) {
		return $this->{'CONFIG'}->{$key}->{'default'};
	}
}

#------------------------------------------------------------------------------------------------------------
#	設定値設定 (0ch+ Only)
#------------------------------------------------------------------------------------------------------------
sub SetConf
{
	my $this = shift;
	my ($key, $val) = @_;
	if ($this->{'is0ch+'}) {
		$this->{'PLUGINCONF'}->SetConfig($key, $val);
	}
	elsif (defined $this->{'CONFIG'}->{$key}) {
		$this->{'CONFIG'}->{$key}->{'default'} = $val;
	}
	else {
		$this->{'CONFIG'}->{$key} = { 'default' => $val };
	}
}

#============================================================================================================
#	Module END
#============================================================================================================
1;
